PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
     ?melding a toezicht:InzendingVoorToezichtMelding ;
            mu:uuid ?uuid ;
            dct:subject ?inzending ;
            adms:status <http://data.lblod.info/melding-statuses/te-behandelen> .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
    ?inzending a toezicht:InzendingVoorToezicht ; mu:uuid ?inzendingUuid .

    FILTER NOT EXISTS { ?s a toezicht:InzendingVoorToezichtMelding ; dct:subject ?inzending }

    BIND(SHA256(CONCAT("1b20ea36-106c-47da-8975-9a13ce5588ec", ":", ?inzendingUuid)) AS ?uuid)
    BIND(IRI(CONCAT("http://data.lblod.info/inzending-voor-toezicht-meldingen/", ?uuid)) as ?melding)
  }
}

;

DELETE {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
     ?melding adms:status <http://data.lblod.info/melding-statuses/te-behandelen> .
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
     ?melding adms:status <http://data.lblod.info/melding-statuses/afgehandeld> .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
    ?inzending a toezicht:InzendingVoorToezicht ;
          mu:uuid ?inzendingUuid ;
          ^dct:subject ?melding .
    ?melding adms:status <http://data.lblod.info/melding-statuses/te-behandelen> .

    FILTER EXISTS { ?inzending nmo:receivedDate ?receivedDate } .
  }
}
