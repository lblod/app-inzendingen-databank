PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>

INSERT {
  GRAPH <http://mu.semte.ch/application> {
     ?melding a toezicht:InzendingVoorToezichtMelding ;
            mu:uuid ?uuid ;
            dct:subject ?inzending ;
            adms:status <http://data.lblod.info/melding-statuses/te-behandelen> .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/application> {
    ?inzending a toezicht:InzendingVoorToezicht ; mu:uuid ?inzendingUuid .

    FILTER NOT EXISTS { ?s a toezicht:InzendingVoorToezichtMelding ; dct:subject ?inzending }

    BIND(SHA256(CONCAT("1b20ea36-106c-47da-8975-9a13ce5588ec", ":", ?inzendingUuid)) AS ?uuid)
    BIND(IRI(CONCAT("http://data.lblod.info/inzending-voor-toezicht-meldingen/", ?uuid)) as ?melding)
  }
}
