PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX meb: <http://rdf.myexperiment.org/ontologies/base/>
PREFIX schema: <http://schema.org/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
     ?review a schema:Review ;
            mu:uuid ?uuid ;
            schema:itemReviewed ?submission ;
            adms:status <http://lblod.data.gift/concepts/d72999bf-2ef4-4332-bd6e-49c8a9e2498e>.
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
    ?submission a meb:Submission ; mu:uuid ?submissionId .

    FILTER NOT EXISTS { ?s a schema:Review ; schema:itemReviewed ?submission }

    BIND(SHA256(CONCAT("1b20ea36-106c-47da-8975-9a13ce5588ec", ":", ?submissionId)) AS ?uuid)
    BIND(IRI(CONCAT("http://data.lblod.info/review-submission/", ?uuid)) as ?review)
  }
}

;

DELETE {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
     ?review adms:status <http://lblod.data.gift/concepts/d72999bf-2ef4-4332-bd6e-49c8a9e2498e> .
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
     ?review adms:status <http://lblod.data.gift/concepts/5b36ed48-491c-4177-ae31-31338dc46de1> .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
    ?submission a meb:Submission ;
          mu:uuid ?submissionId ;
          nmo:sentDate ?sentDate ;
          ^schema:itemReviewed ?review .
    ?review adms:status <http://lblod.data.gift/concepts/d72999bf-2ef4-4332-bd6e-49c8a9e2498e> .

    FILTER EXISTS { ?submission nmo:receivedDate ?receivedDate } .

    FILTER (?sentDate > "2019-04-15T00:00:00"^^xsd:dateTime)
  }
}
